apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: "androidx.navigation.safeargs.kotlin"

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.esp.localjobs"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        // multiDexKeepProguard file(â€˜multi-dex-keep.txt)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        warning 'InvalidPackage'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}
repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' } // geo queries
}

androidExtensions {
    experimental = true
}

configurations {
    ktlint
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.firebaseui:firebase-ui-auth:4.3.1'
    //implementation 'com.firebaseui:firebase-ui-database:4.3.2'
    implementation 'com.google.firebase:firebase-firestore:19.0.0'
    implementation 'com.google.firebase:firebase-auth:17.0.0'

    ktlint "com.pinterest:ktlint:0.32.0"


    //android navigation

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version" // For Kotlin use navigation-fragment-ktx
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version" // For Kotlin use navigation-ui-ktx

    implementation 'com.google.android.material:material:1.1.0-alpha06'


    //recycle view
    implementation 'com.xwray:groupie:2.3.0'
    implementation 'com.xwray:groupie-databinding:2.3.0'
    implementation 'com.xwray:groupie-kotlin-android-extensions:2.3.0'

    implementation('com.mapbox.mapboxsdk:mapbox-android-sdk:7.3.2') {
        exclude group: 'group_name', module: 'module_name'
    }

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'

    // picasso image loader
    implementation 'com.squareup.picasso:picasso:2.71828'


    //dagger
    implementation 'com.google.dagger:dagger:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'




    // location queries
    implementation 'com.github.imperiumlabs:GeoFirestore-Android:v1.2.1'


    //tests
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation 'org.mockito:mockito-core:2.27.0'
    testImplementation 'androidx.test:core:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    debugImplementation 'androidx.fragment:fragment-testing:1.1.0-alpha09'
}

// ktlint
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

apply plugin: 'com.google.gms.google-services'